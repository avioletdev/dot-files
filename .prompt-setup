# Avery's Modular ZSH Prompt
# Lightweight, modular, and performant

# Enable command substitution
setopt PROMPT_SUBST

# Source modules relative to this script
ZSH_CONFIG_DIR="${0:A:h}/zsh"
source "${ZSH_CONFIG_DIR}/theme.zsh"
source "${ZSH_CONFIG_DIR}/utils.zsh"
source "${ZSH_CONFIG_DIR}/git.zsh"
source "${ZSH_CONFIG_DIR}/env.zsh"
source "${ZSH_CONFIG_DIR}/nav.zsh"

# Initialize zoxide (if installed)
if command -v zoxide &> /dev/null; then
    eval "$(zoxide init zsh)"
fi

function build_info_line() {
    local -a segments
    local basedir=$(basename "$(pwd)")
    local pathInfo=$(path_info)
    local last_status=$?

    # Segment 1: User
    segments+="$(wrap_segment "true" "${prompt_colors[one]}" "%n")"
    
    # Segment 2: Path/Project Root
    segments+="$(wrap_segment "false" "${prompt_colors[two]}" "$pathInfo")"

    # Git Info if applicable
    if is_git_repo ; then
        segments+="$(wrap_segment "false" "${prompt_colors[three]}" "$(git_info)")"
        
        # Subfolder info if deep in git repo
        if [[ "$basedir" != "$pathInfo" ]] ; then
            segments+="$(wrap_segment "false" "${prompt_colors[four]}" "$(get_project_subfolder 6 "$pathInfo")")"
        fi
    fi

    # Environment/Project Info
    local env_info=$(project_info)
    if [[ -n "$env_info" ]]; then
        segments+="$(wrap_segment "false" "${prompt_colors[five]}" "$env_info")"
    fi

    echo "${(j::)segments}"
}

# Hooks
preexec() {
    timer_start
}

precmd() {
    local last_status=$?
    # Determine heart color based on last command status
    local heart_color="${prompt_colors[six]}"
    [[ $last_status -ne 0 ]] && heart_color="${prompt_colors[red]}"

    # Command Duration
    local duration=$(timer_stop)
    local timer_str=""
    [[ -n "$duration" ]] && timer_str=" | $duration"

    # Build the info line once per prompt
    INFO_LINE=$(build_info_line)
    
    # Assembly PROMPT
    PROMPT="%k%F{${heart_color}}${symbol_list[round_left]}%K{${heart_color}}%F{${prompt_colors[white]}} ${symbol_list[heart]} $(date '+%H:%M')$timer_str %k%F{${heart_color}}${symbol_list[round_right]} %f|> "
    
    print -P "\n${INFO_LINE}\n"
}
